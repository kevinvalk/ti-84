// Find all points on the Edwards curve
// Kevin Valk, version 1.0.0
// Setup Graph screen
AxesOff
ClrDraw

// Input
Disp "Exp L=R X,Y vars"
Input "L=",Str1
Input "R=",Str2
Input "Mod N=",N

// Do the loop over all points
0→R
For(I,0,N-1)
  For(J,0,N-1)
    // Temp X and Y
    I→X
    J→Y
    
    // Run expression
    N*fPart(expr(Str1)/N)→A
    N*fPart(expr(Str2)/N)→B
    
    // Fix negative remainders
    While A<0
      A+N→A
    End
    While B<0
      B+N→B
    End
    
    // Check if we have a point
    If A=B
    Then
      Text(R,0,"(",X,",",Y,") = ",A)
      Text(R,91,"√")
      R+6→R
    End
    
    // Check if screen is full
    If R+6>62
    Then
      Pause 
      0→R
      ClrDraw
    End
  End
End